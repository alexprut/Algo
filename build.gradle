plugins {
    id 'maven-publish'
    id 'signing'
    id 'java'
    id "jacoco"
    id 'java-library'
    id 'com.github.sherter.google-java-format' version '0.9'
}

group = "com.alexprut"
archivesBaseName = "algo"
version = "v0.4.0"

googleJavaFormat {
    toolVersion = "1.1"
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = true
    }
    dependsOn test
}

tasks.register('javadocJar', Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('ossrhUsername')) {
    signing {
        sign configurations.archives
        sign publishing.publications.mavenJava
    }

    publishing {
        repositories {
            maven {
                name = 'ossrh'
                url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
            maven {
                name = 'ossrhSnapshots'
                url = uri("https://oss.sonatype.org/content/repositories/snapshots/")
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name = 'Algo'
                    packaging = 'jar'
                    description = 'Classic Algorithms and Data Structures implemented in Java'
                    url = 'https://github.com/alexprut/Algo'

                    scm {
                        url = 'https://github.com/alexprut/Algo'
                        connection = 'scm:git:git://github.com/alexprut/Algo.git'
                        developerConnection = 'scm:git:ssh://github.com/alexprut/Algo.git'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/asd/ads/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id = 'alexprut'
                            name = 'Alex Prut'
                        }
                    }
                }
            }
        }
    }
}