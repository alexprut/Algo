plugins {
    id 'maven'
    id 'signing'
    id 'java'
    id "jacoco"
    id 'java-library'
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id 'com.github.sherter.google-java-format' version '0.8'
}

group = "com.alexprut"
archivesBaseName = "algo"
version = "v0.3.0"

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.13'
}

repositories {
    mavenCentral()
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('ossrhUsername')) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Algo'
                    packaging 'jar'
                    description 'Classic Algorithms and Data Structures implemented in Java'
                    url 'https://github.com/alexprut/Algo'

                    scm {
                        connection 'scm:git:git://github.com/alexprut/Algo.git'
                        developerConnection 'scm:git:ssh://github.com/alexprut/Algo.git'
                        url 'https://github.com/alexprut/Algo'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://github.com/alexprut/Algo/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'alexprut'
                            name 'Alex Prut'
                        }
                    }
                }
            }
        }
    }
}